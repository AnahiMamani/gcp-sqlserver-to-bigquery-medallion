config {
    type: "incremental",
    name: "tb_gold_regiao_estado",
    description: "Representa a hierarquia geográfica (região → estado), que é descritiva e usada para filtrar análises regionais .",
    schema: "ds_gas_price_gold",
    tags: ["gold", "dim"],
    uniqueKey: ["estado_id"],
    columns: {
        estado_id: "Chave surrogate única gerada para cada estado",
        regiao: "Nome da região brasileira (NORTE, NORDESTE, CENTRO OESTE, SUDESTE, SUL)",
        estado: "Nome completo do estado (ex.: SÃO PAULO, GOIÁS, ALAGOAS)",
        sigla_estado: "Sigla de duas letras do estado (ex.: SP, GO, AL)",
        ingestao_data: "Data e hora em que o dado foi inserido no sistema"
    }
}

WITH de_para_estados AS (
  SELECT 'ACRE' AS nome, 'AC' AS sigla UNION ALL
  SELECT 'ALAGOAS', 'AL' UNION ALL
  SELECT 'AMAPA', 'AP' UNION ALL
  SELECT 'AMAZONAS', 'AM' UNION ALL
  SELECT 'BAHIA', 'BA' UNION ALL
  SELECT 'CEARÁ', 'CE' UNION ALL
  SELECT 'DISTRITO FEDERAL', 'DF' UNION ALL
  SELECT 'BRASILIA', 'DF' UNION ALL
  SELECT 'ESPÍRITO SANTO', 'ES' UNION ALL
  SELECT 'GOIAS', 'GO' UNION ALL
  SELECT 'MARANHÃO', 'MA' UNION ALL
  SELECT 'MATO GROSSO', 'MT' UNION ALL
  SELECT 'MATO GROSSO DO SUL', 'MS' UNION ALL
  SELECT 'MINAS GERAIS', 'MG' UNION ALL
  SELECT 'PARA', 'PA' UNION ALL
  SELECT 'PARAIBA', 'PB' UNION ALL
  SELECT 'PARANÁ', 'PR' UNION ALL
  SELECT 'PERNAMBUCO', 'PE' UNION ALL
  SELECT 'PIAUI', 'PI' UNION ALL
  SELECT 'RIO DE JANEIRO', 'RJ' UNION ALL
  SELECT 'RIO GRANDE DO NORTE', 'RN' UNION ALL
  SELECT 'RIO GRANDE DO SUL', 'RS' UNION ALL
  SELECT 'RONDONIA', 'RO' UNION ALL
  SELECT 'RORAIMA', 'RR' UNION ALL
  SELECT 'SANTA CATARINA', 'SC' UNION ALL
  SELECT 'SÃO PAULO', 'SP' UNION ALL
  SELECT 'SERGIPE', 'SE' UNION ALL
  SELECT 'TOCANTINS', 'TO'
),

base_distinta AS (
  SELECT 
    tx_regiao AS regiao,
    tx_estado AS estado,
    -- Pegamos a data máxima de ingestão para o estado
    MAX(dt_ingestao) AS dt_ingestao
  FROM `${constants.PROJECT_ID}.${constants.DATASET_SILVER}.tb_silver_producao_etanol_anidro_bep_2012_2018`
  ${when(incremental(), `WHERE dt_ingestao > (SELECT MAX(ingestao_data) FROM ${self()})`)}
  -- Agrupamos apenas por região e estado para eliminar a duplicidade dos anos
  GROUP BY 1, 2
)

SELECT
  -- Agora a chave é gerada apenas sobre a geografia, garantindo unicidade na Gold
  FARM_FINGERPRINT(CONCAT(regiao, estado)) AS estado_id,
  t1.regiao,
  t1.estado,
  COALESCE(t2.sigla, 'UF') AS sigla_estado,
  t1.dt_ingestao AS ingestao_data
FROM
  base_distinta t1
LEFT JOIN
  de_para_estados t2 ON UPPER(t1.estado) = t2.nome