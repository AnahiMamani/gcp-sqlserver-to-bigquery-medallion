config {
    type: "incremental",
    name: "tb_silver_preco_petroleo_mensal",
    schema: "ds_gas_price_silver",
    tags: ["silver", "preco"],
    uniqueKey: ["dt_mes"],
    columns: {
        preco_id: "Identificador único da linha gerado através do hash",
        dt_mes: "Data de referência original",
        dt_mes_dia: "Primeiro dia do mês (DATE)",
        ano: "Ano (INT64)",
        mes: "Mês (INT64)",
        vl_preco_fechamento_petroleo: "Fechamento do petróleo",
        vl_preco_abertura_petroleo: "Abertura do petróleo",
        mx_preco_petroleo: "Máximo do petróleo",
        mn_preco_petroleo: "Mínimo do petróleo",
        vl_volume_petroleo: "Volume transacionado real",
        dt_ingestao: "Data de ingestão"
    }
}

WITH base_limpa AS (
  SELECT
    *,
    REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(Data, 
      'Dez', 'Dec'), 'Out', 'Oct'), 'Set', 'Sep'), 
      'Ago', 'Aug'), 'Mai', 'May'), 'Abr', 'Apr'), 'Fev', 'Feb') AS data_en,
    REPLACE(REPLACE(REPLACE(Vol, 'K',''), 'M', ''),',','.') AS vl_volume_limpo,
    CONTAINS_SUBSTR(Vol, 'K') AS tem_k,
    CONTAINS_SUBSTR(Vol, 'M') AS tem_m
  FROM
    `${constants.PROJECT_ID}.${constants.DATASET_BRONZE}.tb_bronze_preco_petroleo_mensal`
),

base_componentes AS (
  SELECT
    *,
    EXTRACT(YEAR FROM SAFE.PARSE_DATE('%b %y', data_en)) AS ano_extraido,
    EXTRACT(MONTH FROM SAFE.PARSE_DATE('%b %y', data_en)) AS mes_extraido
  FROM
    base_limpa
)

SELECT
    Data AS dt_mes,
    DATE(CAST(ano_extraido AS INT64), CAST(mes_extraido AS INT64), 1) AS dt_mes_dia,
    CAST(ano_extraido AS INT64) AS ano,
    CAST(mes_extraido AS INT64) AS mes,

    SAFE_CAST(REPLACE(`Último`, ',', '.') AS NUMERIC) AS vl_preco_fechamento_petroleo,
    SAFE_CAST(REPLACE(Abertura, ',', '.') AS NUMERIC) AS vl_preco_abertura_petroleo,
    SAFE_CAST(REPLACE(`Máxima`, ',', '.') AS NUMERIC) AS mx_preco_petroleo,
    SAFE_CAST(REPLACE(`Mínima`, ',', '.') AS NUMERIC) AS mn_preco_petroleo,
    CASE
        WHEN tem_k THEN SAFE_CAST(vl_volume_limpo AS NUMERIC)*1000
        WHEN tem_m THEN SAFE_CAST(vl_volume_limpo AS NUMERIC)*1000000 
    END AS vl_volume_petroleo,
    ingestion_timestamp AS dt_ingestao
FROM
    base_componentes

${when(incremental(), `WHERE ingestion_timestamp > (SELECT MAX(dt_ingestao) FROM ${self()})`)}